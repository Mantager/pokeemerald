mapscripts OroTown_HouseC_MapScripts {}

script OroTown_HouseC_EventScript_Trader {
    lock
    faceplayer
    if(flag(FLAG_ORO_TRADE_COMPLETED) == TRUE) {
        msgbox("Thanks!\nTake good care of Finneon!")
        release
        end
    } else {
        setvar(VAR_0x8008, INGAME_TRADE_FINNEON)
        copyvar(VAR_0x8004, VAR_0x8008)
        specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
        copyvar(VAR_0x8009, VAR_RESULT)
        msgbox("I'm looking for a Buizel!\pIf you've got one, want to\ntrade it for a Finneon?", MSGBOX_YESNO)
        if(var(VAR_RESULT) == NO) { goto(OroTown_HouseC_EventScript_TraderDeclined) }
        special(ChoosePartyMon)
        waitstate
        copyvar(VAR_0x800A, VAR_0x8004)
        if(var(VAR_0x8004) == 255) { goto(OroTown_HouseC_EventScript_TraderDeclined) }
        copyvar(VAR_0x8005, VAR_0x800A)
        specialvar(VAR_RESULT, GetTradeSpecies)
        copyvar(VAR_0x800B, VAR_RESULT)
        if(var(VAR_RESULT) == VAR_0x8009) {
            copyvar(VAR_0x8004, VAR_0x8008)
            copyvar(VAR_0x8005, VAR_0x800A)
            special(CreateInGameTradePokemon)
            special(DoInGameTradeScene)
            waitstate
            setflag(FLAG_ORO_TRADE_COMPLETED)
            msgbox("Thank you so much!")
        } else {
            msgbox("Hey, I'm not dumb!\nThat's not a Buizel at all!")
        }
        release
        end
    }
}

script OroTown_HouseC_EventScript_TraderDeclined {
    msgbox("You don't want to trade?\pI'll ask someone else then.")
    release
    end
}