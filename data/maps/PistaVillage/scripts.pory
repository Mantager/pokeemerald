mapscripts PistaVillage_MapScripts {}

script PistaVillage_Trigger_PlayerMeetFern {
    lockall
    message("{COLOR}{RED}Clad!")
    closemessage
    getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
    setobjectxy(1, 9, VAR_TEMP_2)
    setobjectxyperm(1, 9, VAR_TEMP_2)
    applymovement(1, PistaVillage_Movement_WalkLeft7)
    waitmovement(0)
    delay(8)
    msgbox("Fern.\nWhat's up?\lWhere are the others?")
    msgbox("{COLOR}{RED}Some weird old guy invited Rye\nto check out his 'dojo',\nand he went along with him!\pMalimo tried to discourage him,\nbut Rye just went along\land left anyway!")
    msgbox("So what?\nWhat's the problem?")
    msgbox("{COLOR}{RED}Well Clad, we are supposed to be\ntravelling to Wing Peak, if\lyou can remember that far back!")
    msgbox("We must be pretty close to\nWing Peak now though, right?\pAnd Nas is gone, so he's not\ngoing to 'steal our wishes'.")
    msgbox("{COLOR}{RED}I guess you've got a pointâ€¦")
    msgbox("Let's rest up before we head off.\pThe climb to the top\nof Wing Peak will be tough.\pBesides, I kind of want to\nsee this dojo for myself.\pIs it in Pista Village?")
    closemessage
    applymovement(1, Common_Movement_FaceDown)
    waitmovement(0)
    msgbox("{COLOR}{RED}Yup, it's right down there.")
    closemessage
    applymovement(1, Common_Movement_FaceLeft)
    waitmovement(0)
    msgbox("{COLOR}{RED}Maybe I've been too hasty on\nthis trip.\pI agree, let's take a break\nbefore the big climb.\pSee you in a bit!")
    closemessage
    fadescreen(1)
    removeobject(1)
    setflag(FLAG_PISTA_VILLAGE_HIDE_FERN)
    setvar(VAR_PISTA_VILLAGE_STATE, 1)
    fadescreen(0)
    delay(8)
    releaseall
    end
}

script PistaVillage_Trigger_NeedToVisitDojo {
    lock
    if(var(VAR_PISTA_VILLAGE_STATE) == 1) { msgbox("(I should probably go and check\nout the dojo before I leave.)")
    } elif(var(VAR_PISTA_VILLAGE_STATE) == 2) { msgbox("(I shouldn't just ditch Sanchez\nlike that.)") }
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
    waitmovement(0)
    release
    end
}

movement PistaVillage_Movement_WalkLeft7 {
    walk_left * 7
    step_end
}